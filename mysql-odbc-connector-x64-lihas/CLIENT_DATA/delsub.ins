; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib gmbh
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/credits/

; All these installers use different GUIDs
; e.g.
; 5.1.13 x86 - {EDB9F166-FA9B-4F71-B2C8-BECD561BD77F}
; 5.3.6 x64 -  {17E48BE8-F0F8-42B6-82D3-7A5840694D79}
; 5.3.6 x86 -  {75E9C88D-3B4E-44A7-967F-AE22A210167C}
; So the traditional way of uninstalling an MSI using OPSI does not work
; Instead, we're cheating a little - there's a file matching
; dir C:\Windows\Installer\MySQL*.* /s /b
; and the result contains a directory name with the GUID we're looking for

Message "Uninstalling " + $ProductId$ + " ..."
 
comment "Starting uninstall"
DosInAnIcon_uninstall /SysNative
sub_check_exitcode
ExitWindows /Reboot
 
[DosInAnIcon_uninstall]
for /F "tokens=4 delims=\" %%A in ('dir C:\Windows\Installer\MySQL*.* /s /b') do (
set MSI=%%A
msiexec.exe /x %%A /qb REBOOT=ReallySuppress
)
REM this is so a pristine installation does't fail -
REM as the installer tries to run this batch as well
REM and if it's the first time, there's obviously no
REM older version to uninstall, so the above command
REM is bound to fail with a "file not found" message
if "%MSI%" == "" exit /b 0

[Sub_check_exitcode]
comment "Test for installation success via exit code"
set $ExitCode$ = getLastExitCode
; informations to exit codes see
; http://msdn.microsoft.com/en-us/library/aa372835(VS.85).aspx
; http://msdn.microsoft.com/en-us/library/aa368542.aspx
if ($ExitCode$ = "0")
	comment "Looks good: setup program gives exitcode zero"
else
	comment "Setup program gives a exitcode unequal zero: " + $ExitCode$
	if ($ExitCode$ = "1605")
		comment "ERROR_UNKNOWN_PRODUCT	1605	This action is only valid for products that are currently installed."
		comment "Uninstall of a not installed product failed - no problem"
	else
		if ($ExitCode$ = "1641")
			comment "looks good: setup program gives exitcode 1641"
			comment "ERROR_SUCCESS_REBOOT_INITIATED	1641	The installer has initiated a restart. This message is indicative of a success."
		else
			if ($ExitCode$ = "3010")
				comment "looks good: setup program gives exitcode 3010"
				comment "ERROR_SUCCESS_REBOOT_REQUIRED	3010	A restart is required to complete the install. This message is indicative of a success."
			else
				logError "Fatal: Setup program gives an unknown exitcode unequal zero: " + $ExitCode$
				isFatalError
			endif
		endif
	endif
endif
