; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/credits/
 
[Actions]
requiredWinstVersion >= "4.10.8.6"
 
DefVar $UninstallProgram$
DefVar $LogDir$
DefVar $ProductId$  
DefVar $MinimumSpace$
DefVar $InstallDir$
DefVar $ExitCode$
DefVar $VersionMajorMinor$
DefVar $OfficeMsiId$
DefVar $Bittyness$
DefVar $LatestVersion$
DefVar $ConnectorVersion$
DefVar $INST_SystemType$

Set $OfficeMsiId$ = "{" +  GetProductProperty("OfficeMsiId","90140000-0011-0000-1000-0000000ff1ce") + "}"

Set $Bittyness$ = GetProductProperty("Bittyness","dependsonoffice")
Set $LatestVersion$ = "Latest"
Set $ConnectorVersion$ = GetProductProperty("ConnectorVersion","Latest")
Set $INST_SystemType$ = GetSystemType

if $ConnectorVersion$ = "Latest"
	Set $ConnectorVersion$ = $LatestVersion$
endif

Set $VersionMajorMinor$ = takestring(0,splitstring($ConnectorVersion$,".")) + "." + takestring(1,splitstring($ConnectorVersion$,"."))

Set $LogDir$ = "%SystemDrive%\tmp"
 
; ----------------------------------------------------------------
; - Please edit the following values                             -
; ----------------------------------------------------------------
;$ProductId$ should be the name of the product in opsi
; therefore please: only lower letters, no umlauts, 
; no white space use '-' as a seperator
Set $ProductId$       = "mysql-odbc-connector-x64-lihas"
Set $MinimumSpace$    = "100 MB"
; the path were we find the product after the installation
; ----------------------------------------------------------------

if not($INST_SystemType$ = "64 Bit System")
	LogError "Wrong OS Architecture (x64 required) for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed

else
 
	if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
		LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
		isFatalError
		; Stop process and set installation status to failed
	else
		comment "Show product picture"
		ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $ProductId$
	 
		if FileExists("%ScriptPath%\delsub.ins")
			comment "Start uninstall sub section"
			Sub "%ScriptPath%\delsub.ins"
		endif
	 
		Message "Installing " + $ProductId$ + " ..."
	 
		comment "Start setup program"
		if ($Bittyness$ ="dependsonoffice" and GetRegistryStringValue64("[HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\" + $OfficeMsiId$ + "] DisplayName") = "" and GetRegistryStringValue32("[HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\" + $OfficeMsiId$ + "] DisplayName") = "")
			; No office found
			Set $Bittyness$ = "sysnative"
		endif

	    if (($INST_SystemType$ = "64 Bit System" and $Bittyness$ ="sysnative") or ($INST_SystemType$ = "64 Bit System" and $Bittyness$ ="dependsonoffice" and not(GetRegistryStringValue64("[HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\" + $OfficeMsiId$ + "] DisplayName") = "")))
			Set $Bittyness$ = "x64"
			Set $InstallDir$      = "%ProgramFiles64Dir%\MySQL\Connector ODBC " + $VersionMajorMinor$
			Winbatch_install /64Bit
		else
			Set $Bittyness$ = "32"
			Set $InstallDir$      = "%ProgramFiles32Dir%\MySQL\Connector ODBC " + $VersionMajorMinor$
			Winbatch_install /32Bit
		endif
		ExitWindows /Reboot
		Sub_check_exitcode

	endif
endif

[Winbatch_install]
msiexec /i "%ScriptPath%\files\mysql-connector-odbc-$ConnectorVersion$-win$Bittyness$.msi" /l* "$LogDir$\$ProductId$.install_log.txt" /qn ALLUSERS=2 REBOOT=ReallySuppress
 
[Sub_check_exitcode]
comment "Test for installation success via exit code"
set $ExitCode$ = getLastExitCode
; informations to exit codes see
; http://msdn.microsoft.com/en-us/library/aa372835(VS.85).aspx
; http://msdn.microsoft.com/en-us/library/aa368542.aspx
if ($ExitCode$ = "0")
	comment "Looks good: setup program gives exitcode zero"
else
	comment "Setup program gives a exitcode unequal zero: " + $ExitCode$
	if ($ExitCode$ = "1605")
		comment "ERROR_UNKNOWN_PRODUCT	1605	This action is only valid for products that are currently installed."
		comment "Uninstall of a not installed product failed - no problem"
	else
		if ($ExitCode$ = "1641")
			comment "looks good: setup program gives exitcode 1641"
			comment "ERROR_SUCCESS_REBOOT_INITIATED	1641	The installer has initiated a restart. This message is indicative of a success."
		else
			if ($ExitCode$ = "3010")
				comment "looks good: setup program gives exitcode 3010"
				comment "ERROR_SUCCESS_REBOOT_REQUIRED	3010	A restart is required to complete the install. This message is indicative of a success."
			else
				logError "Fatal: Setup program gives an unknown exitcode unequal zero: " + $ExitCode$
				isFatalError
			endif
		endif
	endif
endif

